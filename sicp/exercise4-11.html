<!DOCTYPE html>
<html lang="en">
<head>

  <meta charset="utf-8">
  <title>SICP</title>
  <meta name="description" content="Junsong's blog">
  <meta name="author" content="Junsong Li">

  <meta name="viewport" content="width=device-width, initial-scale=1">

  <link href="//fonts.googleapis.com/css?family=Raleway:400,300,600" rel="stylesheet" type="text/css">
  <link href="https://fonts.googleapis.com/css?family=Fira+Code&display=swap" rel="stylesheet">

  <link href="/assets/normalize.css" rel="stylesheet" type="text/css">
  <link href="/assets/skeleton.css" rel="stylesheet" type="text/css">
  <link href="/assets/main.css" rel="stylesheet" type="text/css">
  <link href="/assets/template282.css" rel="stylesheet" type="text/css">
  <link href="/sicp/sicp.css" rel="stylesheet" type="text/css">

  <script src="/assets/simple-jquery.js"></script>
  <script src="/assets/dropdown.js"></script>

  <link rel="icon" type="image/png" href="images/favicon.png">
</head>
<body>

  <div class="container book-header">
    <h2 class="book-title"> SICP 全笔记 </h1>
    <nav>
      <ul class="book-navbar">
        <li><a href="/sicp/index.html">目录</a></li>
        <li><a href="/sicp/about.html">关于</a></li>
      </ul>
    </nav>

  </div>

  <div class="container page">
    <div class="row">
      <div class="offset-by-two eight columns">
        <div class="content">
          <div class="exerciseQ">Exercise 4.11.  Instead of representing a frame as a pair of lists, we can represent a frame as a list of bindings, where each binding is a name-value pair. Rewrite the environment operations to use this alternative representation.</div><p> 对于一段代码，比如</p><pre class="gist"><code>(define x 1)
(define y 2)
(define z 3)
</code></pre><p> 我们的 frame 现在是这个样子的：</p><pre class="gist"><code>((x y z) (1 2 3))
</code></pre><p> 现在应该把它更改为：</p><pre class="gist"><code>((x . 1) (y . 2) (z . 3))
</code></pre><p>  这就只需要在 <span class="vsymbol v">make-frame</span> 的时候把 variables 和 values 使用 map 组合起来就可以。</p><pre class="gist sicp-code"><code>(define (make-frame variables values)
  (map (lambda (x y) (cons x y)) variables values))
</code></pre><p>采用现在的表示形式，我们的 <span class="vsymbol v">add-binding-to-frame!</span> 也需要变化。为了保证能够修改传递到过程中的参数，我们只能使用 <span class="vsymbol v">set-cdr!</span> 去修改 frame 的最后一个 pair 的指针。</p><pre class="gist sicp-code"><code>(define (add-binding-to-frame! var val frame)
  (define (last-pair lst)
    (if (or (null? lst) (null? (cdr lst)))
        lst
        (last-pair (cdr lst))))
  (set-cdr! (last-pair frame) (list (cons var val))))
</code></pre><p>; 在变更了 frame 的表示方式之后，与 env 的底层有关系的操作都需要变更。包括</p><ol><li><code>lookup-variable-value</code></li><li><code>set-variable-value!</code></li><li><code>define-variable!</code></li></ol><pre class="gist sicp-code"><code>(define (lookup-variable-value var env)
  (define (lookup-frame frame) 
    (cond ((null? frame)
           (lookup-variable-value var (enclosing-environment env)))
          ((eq? var (car (car frame)))
           (cdr (car frame)))
          (else
           (lookup-frame (cdr frame)))))
  (if (eq? env the-empty-environment)
      (error 'lookup-variable-value "Unbound variable" var)
      (lookup-frame (first-frame env))))

;;; 4.
(define (set-variable-value! var val env)
  (define (lookup-and-set-frame frame)
    (cond ((null? frame)
           (set-variable-value! var val (enclosing-environment env)))
          ((eq? var (car (car frame)))
           (set-cdr! (car frame) val))
          (else
           (lookup-and-set-frame (cdr frame)))))
  (if (eq? env the-empty-environment)
      (error 'set-variable-value! "Unbound variable -- SET!" var)
      (lookup-and-set-frame (first-frame env))))

;;; 5.
(define (define-variable! var val env)
  (define (lookup-and-set-frame frame)
    (cond ((null? frame)
           (add-binding-to-frame! var val (first-frame env)))
          ((eq? var (car (car frame)))
           (set-cdr! (car frame) val))
          (else
           (lookup-and-set-frame (cdr frame)))))
  (lookup-and-set-frame (first-frame env)))
</code></pre><p>修改完之后，我们使用之前就写好的 regression test 来进行测试：</p><pre class="gist sicp-code"><code>;(load "evaluator.scm")
(load "../testframe.scm")

;;; self-evaluating
(assert= (eval 1 the-global-environment) 1)
(assertequal? (eval "str" the-global-environment) "str")

;; definition
(eval '(define var1 1) the-global-environment)
(assert= (eval 'var1 the-global-environment) 1)

(eval '(define var-special false) the-global-environment) ;; for testing of env lookup 
(asserteq? (eval 'var-special the-global-environment) false)

(eval '(define (empty? lst) (null? lst))
      the-global-environment)

(eval '(define (new-append x y)
         (if (null? x)
             y
             (cons (car x)
                   (new-append (cdr x) y)))) the-global-environment)

(asserteq? (eval '(empty? '(1 2)) the-global-environment)
           false)
(assertequal? (eval '(new-append '(1 2) '(3 4)) the-global-environment)
              '(1 2 3 4))

;; assignment
(asserteq? (eval '(set! var1 2) the-global-environment) 'ok)
(assert= (eval 'var1 the-global-environment) 2)

(assert/exn (eval 'var2 the-global-environment) "Unbound")
(assert/exn (eval '(set! var2 34) the-global-environment) "Unbound")

;; if
(assert= (eval '(if 0 1 2) the-global-environment) 1)
(assert= (eval '(if 'x 1 2) the-global-environment) 1)
(assert= (eval '(if '() 1 2) the-global-environment) 1)
(assert= (eval '(if false 1 2) the-global-environment) 2)

;; cond
(assert= (eval '(cond ((= var1 2) 19)) the-global-environment) 19)
(assert= (eval '(cond ((= var1 1) 19)
                      ((= var1 2) 20)) the-global-environment) 20)
(asserteq? (eval '(cond ((= var1 1) 19)) the-global-environment) false)


;; lambda
(asserteq? (eval '((lambda (x) (= x 1)) 1) the-global-environment) true)

;; begin
(assert= (eval '(begin (set! var1 14)
                       (set! var1 (+ var1 1))
                       var1) the-global-environment) 15)</code></pre>
        </div>
      </div>
    </div>

  </div>

</body>
</html>